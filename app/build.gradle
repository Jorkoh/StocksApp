plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.5.10"
}

apply from: "$rootDir/versions.gradle"
apply plugin: 'kotlin-android'

android {
    compileSdkVersion versions.compile_sdk

    defaultConfig {
        applicationId "com.example.stocksapp"
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.compile_sdk
        versionCode versions.version_code
        versionName versions.version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "IEX_PUBLISHABLE_TOKEN", "\"" + getPropertyOrDefault("IEX_PUBLISHABLE_TOKEN", "") + "\""
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        // Enable Coroutines, Flow and Compose animation APIs
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }
}

dependencies {

    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.compose.ui:ui:$versions.compose"
    implementation "androidx.compose.material:material:$versions.compose"
    implementation "androidx.compose.ui:ui-tooling:$versions.compose"
    implementation "androidx.compose.ui:ui-util:$versions.compose"
    implementation "androidx.activity:activity-compose:$versions.compose_activity"
    implementation "androidx.navigation:navigation-compose:$versions.compose_navigation"

    // Accompanist utilities
    implementation "com.google.accompanist:accompanist-insets:$versions.accompanist"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$versions.accompanist"

    // Coil
    implementation "io.coil-kt:coil-compose:$versions.coil"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutines"

    // DI
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    implementation "androidx.hilt:hilt-navigation-compose:$versions.hilt_navigation"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"

    // Network stuff
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$versions.kotlinx_serialization"
    implementation "com.github.skydoves:sandwich:1.0.9"
    implementation "com.squareup.moshi:moshi-kotlin:$versions.moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"

    // Room
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}

Object getPropertyOrDefault(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}